# The configure.ac File

Make sure you read the [introduction to the autotools](./autotools-intro) first.
[Here you can find the full autoconf documentation](http://www.gnu.org/software/autoconf/manual/).

The purpose of the configuration script is to test everything required for the compilation of the project's source code, and to set up all Makefiles.


## General Concepts

Autoconf works by running the macro expansion program `m4`, using the `configure.ac` file as input.
This file contains **macros** that are expanded (recursively) until, in the end, a pure shell script for `sh` is created.
The `configure.ac` input file can also contain **`sh` commands**, which will appear literally in the final `configure` script.

Everything in a line following a "`#`" is a **comment**.
Comments are usually copied into the generated `configure` script, unless the line starts with at least two "`#`".

By convention, macro names are capitalized, and they start with **`AC_`** if the macro is an `autoconf ` macro, **`AX_`** if it's an `autoconf-archive` macro, and **`AM_`** if it's an `automake` macro.
We provide additional custom macros for COIN-OR configuration (in the `coin*.m4` files), which start with **`AC_COIN_`**.
In the [autoconf documentation](http://www.gnu.org/software/autoconf/manual/)  and the [autoconf-archive documentation](https://www.gnu.org/software/autoconf-archive/The-Macros.html#The-Macros) you can find a description of the predefined autoconf macros.

Like subroutines, macros can be written to have parameters and be invoked with arguments for those parameters.
Parameters and arguments are separated by commas.
The **quotation symbols** for `autoconf` are the square brackets "`[`" and "`]`".
If a single argument contains a comma, the argument must be quoted by enclosing it in brackets.
If in doubt, use quotation.
If a macro is written to use four parameters, but only two arguments are provided, then the last two parameters are assumed to be unset (equivalent to an argument of `[]`).


## Beginning of a `configure.ac` file

At the beginning of a `configure.ac` file in COIN-OR you will find something like the following:

```
## Copyright (C) 2011 International Icecream Machines.
# All Rights Reserved.
# This file is distributed under the Eclipse Public License 2.0.
#
# Author:  John Doe                     IIM    2011-04-01

#############################################################################
#                       Names and other basic things                        #
#############################################################################

AC_PREREQ(2.59)

AC_INIT([SuperSolver],[1.2.1],[https://github.com/coin-or/SuperSolver/issues/new],,[https://github.com/coin-or/SuperSolver])

AC_COPYRIGHT([
Copyright 2011 International Icecream Machines and others.
All Rights Reserved.
This file is part of the open source package COIN-OR which is distributed
under the Eclipse Public License 2.0.])

# List one file in the package so that the configure script can test
# whether the package is actually there
AC_CONFIG_SRCDIR(src/SuperSolverMain.cpp)

# Do some project-level initialization work (version numbers, ...)
AC_COIN_INITIALIZE
```

 * The file should contain the **copyright notice**, information about the **authors**, and state under what **license** the file is made available.

 * The **`AC_PREREQ`** macro specifies the version number of `autoconf` that is required to generate a `configure` script from this input file.
   In COIN-OR, we ask people to [use exactly the same versions of all the GNU autotools](./get-autotools), so that we can collectively take care of bug fixes and can avoid the situation where different versions of the autotools generate large differences in the generated configuration files when multiple developers work on a project simultaneously.

 * The **`AC_INIT`** macro takes as arguments the name of the project, its version number, contact information in case a user wants to report a bug, a tar ball name (omitted here, which results in using 'supersolver'), and a URL.
   The name and version number determine the name of the compiler defines for `config.h` that capture the version number (`SUPERSOLVER_VERSION`, `SUPERSOLVER_VERSION_MAJOR`, `SUPERSOLVER_VERSION_MINOR`, `SUPERSOLVER_VERSION_RELEASE`).

 * The argument of the **`AC_COPYRIGHT`** macro becomes the copyright notice in the generated `configure` script.

 * The **`AC_CONFIG_SRCDIR`** helps the `configure` script to do a sanity check.
   When the shell commands generated by expanding `AC_CONFIG_SRCDIR` are executed, the `configure` script tests if it is in the correct location with respect to the rest of the package.  As the argument, one provides a file (such as a source file) that belongs to the package.

 * The **`AC_COIN_INITIALIZE`** macro does several COIN-OR specific initializations, like setting automake options, getting the build and host type, setting up preprocessor defines for the projects version number, etc.

It is **possible to give a shell command as an argument for a macro**.
Don't forget that `autoconf` and `automake` only perform macro expansion.
Shell commands used as arguments to `autoconf` macros are just text strings to `autoconf`.
The shell command will not be executed until the generated `configure` script is executed.
**This is a feature!**


## The Body of the `configure.ac` File

After the initialization described above, `configure.ac` usually contains a number of **macros that will be expanded into the tests** that are to be run by the `configure` script.
In general, one
  * checks for the **availability and names of programs** (such as compilers and other tools),
  * tests for the presence of **header files, libraries, _etc._**, including other COIN-OR projects,
  * executes other project specific tests and settings (such as the byte size of a type, _etc._),
  * sets the values of `autoconf` output variables and [configuration header](./autotools-intro) `#define`s, and
  * generates links to files required for unit tests or example programs in case of a VPATH configuration.

Autoconf output variables are specified with the macro invocation **`AC_SUBST(VARNAME)`**, where `VARNAME` is the name of the output variable.
The value of the output variable will be the value of the corresponding shell variable in the `configure` script.
Therefore, setting the value of an output variable is done with a shell command like
```
VARNAME="value1 and maybe a few more"
```
**The `sh` shell does not allow spaces before and after the `=` symbol!**

In order to include a "`#define`" into the configuration header file that the `configure` script is going to create, one uses the **`AC_DEFINE`** and **`AC_DEFINE_UNQUOTED`** macros, see the [autoconf documentation](http://www.gnu.org/software/autoconf/manual/).


In the following we describe the individual parts of the `configure.ac` file body in more detail and present examples.


### Initialization of Tools and Compilers

This part usually looks like this:

```
#############################################################################
#                         Standard build tool stuff                         #
#############################################################################

# Get the name of the C, C++, and Fortran compilers and appropriate compiler options.
AC_COIN_PROG_CC
AC_COIN_PROG_CXX
AC_COIN_PROG_F77

# If there is a Fortran compiler, then setup everything to use it, including F77_FUNC
if test -n "$F77" ; then
  AC_COIN_F77_SETUP
fi

# Initialize libtool
AC_COIN_PROG_LIBTOOL

# set RPATH_FLAGS to the compiler link flags required to hardcode location
# of the shared objects (expanded_libdir is set somewhere in configure before)
AC_COIN_RPATH_FLAGS([$expanded_libdir])

# Get the C++ runtime libraries in case we want to link a static library
# with a C or Fortran compiler
AC_COIN_CXXLIBS

# Doxygen
AC_COIN_DOXYGEN

# SUPERSOLVER_VERBOSITY and SUPERSOLVER_DEBUGLEVEL
AC_COIN_DEBUGLEVEL
```

 * The macros **`AC_COIN_PROG_CC`**, **`AC_COIN_PROG_CXX`**, **`AC_COIN_PROG_F77`**, and **`AC_COIN_PROG_FC`** determine the name of the C, C++, Fortran 77, and Fortran 90 compilers, and choose the default compiler options.
   One only needs to specify those compilers that are required to compile the source code in the project.
   If the source code does not contain Fortran source, one should omit **`AC_COIN_PROG_F77`** and **`AC_COIN_PROG_FC`**.
   
 * The macro **`AC_COIN_F77_SETUP`** determines variables required to compile Fortran object files and invoke Fortran functions from C/C++ code.
   This has been separated out from **`AC_COIN_PROG_F77`** to allow a projects configure to succeed even if a Fortran compiler is not found (in which case building of Fortran code must be omitted).

 * The macro **`AC_COIN_PROG_LIBTOOL`** executes a number of tests, and then creates the [libtool script](./autotools-intro).
 
 * The macro **`AC_COIN_RPATH_FLAGS`** defines a variable `RPATH_FLAGS` that can be used by the linker to hardwire the library search path for the given directories into a shared library.
   This is useful to setup example Makefiles, but not used for the projects own libraries or binaries.

 * The macro **`AC_COIN_CXXLIBS`** stores the C++ runtime libraries required for linking a C++ library with a Fortran or C compiler in variable `CXXLIBS`.
   Most projects do not need this macro.

 * The macro **`AC_COIN_DOXYGEN`** macro is used to initialize variables that are used in the doxygen configuration file of the project. See [here](./doxygen) for more information on using Doxygen in COIN-OR.

 * The macro **`AC_COIN_DEBUGLEVEL`** makes the `--with-`_prjct_`-verbosity`, and `--with-`_prjct_`-checklevel` available for `configure`, which allow a finer project-specific control of debug and verbosity levels.


### Check for Packages, e.g., other COIN-OR packages

This part looks like this:
```
AC_COIN_CHK_PKG(CoinUtils,[ClpLib OsiClpUnitTest])
if test $coin_has_coinutils != yes ; then
  AC_MSG_ERROR([Required package CoinUtils not available.])
fi
AC_COIN_CHK_PKG(Osi,[OsiClpLib OsiClpUnitTest])
AC_COIN_CHK_PKG(OsiTests,[OsiClpUnitTest],[osi-unittests])
AC_COIN_CHK_PKG(Sample,,[coindatasample],[],dataonly)
AC_COIN_CHK_PKG(Netlib,,[coindatanetlib],[],dataonly)
```

Here, for each package that is required to compile the libraries and programs in this project (including unit tests and example programs), we list their names as arguments of an invocation of **`AC_COIN_CHK_PKG**`.
This example is taken from Clp.

The arguments have the following meaning:

1. The first argument is the **name of the "package"**.
1. The second argument can be used to specify particular libraries or binaries to which the dependency applies, in this case `ClpLib` and `OsiClpUnitTest`.
1. The third argument specifies the **name of the pkg-config file associated with the package**.
   For the pkg-config file argument, it should also be possible to specify version requirements, e.g., `coinutils >= 2.11`, which pkg-config will then use to check that the right version of CoinUtils is available.
1. The fourth argument can typically be left empty and is only used in very specific situations.
1. The fifth argument can specify additional options, in particular whether the package provides no data (the default) or only data (`dataonly` as used for `Sample` and `Netlib`).
   This is used to reduce the number of configure options (`--with-`_prjct_`-*`) and autoconf variables created by this macro.

The `AC_COIN_CHK_PKG` is setup to skip packages that are specified in the `COIN_SKIP_PROJECTS` variable or for which `--without-`_pkg_ has been specified as configure option.

If a package is not to be skipped, the macro first checks whether the **user has specified compiler or linker flags or a data directory** via the `--with-`_pkg_`-lflags`/`-cflags`/`-data` or `--with-`_pkg_` options.

Then, if pkg-config is available, it is used to check for the existence of the specified project, unless `skip` has been specified as third argument.
If the third argument has been given (and is not `skip`), it is passed as is to pkg-config. Otherwise, the package name in lower case will be used.
For the pkg-config call, the package search path is prefixed with the `pkg-config` subdirectory of the library installation directory of the project, i.e., `$libdir/pkg-config`, which is to simplify build and installation of a series of packages under the same `$prefix`.

If the package is found (either via pkg-config or because `--with-`_pkg_`-*` flags were given), then a number of variables are setup:
  * A `#define` will be set in the configuration header files with the name `PROJECT_HAS_PACKAGE`,
    where `PROJECT` is replaced by the name of the project that is configured (first argument to `AC_INIT` in all capital letters)
    and `PACKAGE` is replaced by the name of the package in all capital letters.
    That is, for the first `AC_COIN_CHK_PKG` in the example, `CLP_HAS_COINUTILS` will be defined.
  * An Automake conditional with the name `COIN_HAS_PACKAGE`, where `PACKAGE` is replaced by the name of the package in all capital letters, is set to true.
  * The shell variable `coin_has_`_pkg_ will be set to `yes`.
  * The **compiler and linker flags, pkg-config dependencies, and data directories are accumulated** for each library specified in the second argument.
    * C/C++ compiler flags are stored in various `X_CFLAGS` variables.
    * Linker flags are stored in `X_LFLAGS` variables.
    * pkg-config dependencies are stored in `X_PCFILES` variables (they will be necessary to setup `.pc` files).
    * A directory where a package's data can be found is stored in a `X_DATA` variable.
    However, **at this point**, variables `X_LFLAGS` and `X_CFLAGS` only store information that was specified via `--with-`_pkg_`-lflags`/`-cflags`, **not values that are given via pkg-config files**.
    In a later use of the macro `AC_COIN_FINALIZE_FLAGS`, the variables `X_LFLAGS` and `X_CFLAGS` are updated (see below).
  
    In the example, the following variables for use in Makefiles are created:
    * `CLPLIB_CFLAGS`, `CLPLIB_LFLAGS`, `CLPLIB_PCFILES`,
    * `OSICLPLIB_CFLAGS`, `OSICLPLIB_LFLAGS`, `OSICLPLIB_PCFILES`,
    * `OSICLPUNITTEST_CFLAGS`, `OSICLPUNITTEST_LFLAGS`, `OSICLPUNITTEST_PCFILES`,
    * `SAMPLE_DATA`,
    * `NETLIB_DATA`
    For ClpLib, the corresponding values could be
    * `CLPLIB_CFLAGS="-I/my/build/of/mumps/include -I/my/build/of/mumps/libseq"`
    * `CLPLIB_LFLAGS="-L/my/build/of/mumps/lib -lmumps"`
    * `CLPLIB_PCFILES=coinutils`

If the package is not found, the configuration does not end with an error, but also sets up variables:
  * The Automake conditional with the name `COIN_HAS_PACKAGE`, where `PACKAGE` is replaced by the name of the package in all capital letters, is set to false.
  * The shell variable `coin_has_`_pkg_ will be set to `no` or `skipping`.   
    Thus, this variable can be used in configure to check whether a package could be found and stop with an error if a required package was not found.
    In this example, CoinUtils is required, while Osi, OsiTests, Sample, and Netlib are optional.


### Check for Libraries

A macro similar to `AC_COIN_CHK_PKG` is provided to test the availability of libraries that do not provide a pkg-config file, called **`AC_COIN_CHK_LIB`**.
A typical invocation looks like this:

```
AC_COIN_CHK_LIB(Cplex,[OsiCpxLib OsiTest],[-lcplex -lpthread -lm -ldl],[],[],[CPXgetstat])
```

The arguments have the following meaning:

 1. _Name of the library package_.
    This is the name as it will appear in the `configure` output (all capitals) and in configure `--with` options (all lower case).
 1. _List of targets for which the user library is a dependency_.
    This argument can be used to specify a list of libraries or binaries which will have the user library as dependency (if available).
    For each such target X, the variables `X_LFLAGS` and `X_CFLAGS` are augmented with the linker and compiler flags specified by `--with-`_userlibrary_`-lflags`/`-cflags`.
 1. _Default linker flags_.
    Flags to link against the library package if not specified by user via `--with-`_userlibrary_`-lflags`.
 1. _Default compiler flags_.
    Flags to compile against the library package if not specified by user via `--with-`_userlibrary_`-cflags`.
 1. _Default data directory_.
    Default directory where to expect package data if not specified by user via `--with-`_userlibrary_`-data`.
 1. _Name of a C function in the library_.
    This should be the name of a C function that should be defined in the library.
    If the user uses the `--with-`_userlibrary_`-lflags` flag, a test is performed to confirm that this function is indeed available.
    If it is not available, the `configure` script will fail with an error message.
 1. _Linker flags required for library check_.
    This argument can be used to specify additional linker flags that are required for checking whether the C function given in the fifth argument is available.
 1. The eigth argument can typically be left empty and is only used in very specific situations.
 1. _Additional options_.
    This argument is used in particular to specify whether the package provides no data (the default) or only data (`dataonly`).
    This is used to reduce the number of configure options (`--with-`_userlibrary_`-*`) and autoconf variables created by this macro.

After completion of the tests, the variables are setup in the same way as for `AC_COIN_CHK_PKG`.
`X_PCFILES` are not created or modified here, but `X_LFLAGS`, `X_CFLAGS`, `X_DATA` are updated.

### Checks for some specific System Libraries

#### Checks for Math Library, ZLib, BZlib, Readline, and GMP

Special COIN-OR macros are available to check for often used libraries:

 * **`AC_COIN_CHK_LIBM`**: Check for the math library (e.g., `libm.so` on Linux systems).
 * **`AC_COIN_CHK_BZLIB`**: Check for the bzlib compression library.
 * **`AC_COIN_CHK_GMP`**: Check for the GNU multiple precission library.
 * **`AC_COIN_CHK_GNU_READLINE`**: Check for the GNU readline library.
 * **`AC_COIN_CHK_ZLIB`**: Check for the zlib compression library.

Each macro expects as argument a list of libraries to which this dependencies applies to, e.g.,
```
AC_COIN_CHK_LIBM(CoinUtilsLib)
```
If the package is available, it adds the flags required for linking to the `X_LFLAGS` variable, where `X` iterates again over all words given in the first argument of the macro in capitial letters.
Except for AC_COIN_CHK_LIBM, the macros also
* `#define` the appropriate preprocessor symbol (e.g., `COINUTILS_HAS_ZLIB`) in the configuration header file,
* define an Automake conditional (e.g., `COIN_HAS_ZLIB`), and
* define a shell variable (e.g., `coin_has_zlib`),
* add a configure option to disable the check for the library (e.g., `--disable-zlib`).

Note, that CoinUtils already checks for the math library and thus every project using CoinUtils automatically has the math library in its dependencies and thus does not need to check for it separately.

#### Check for LAPACK

For Lapack, a customized macro `AC_COIN_CHK_LAPACK` has been setup.

I a user did not specify flags for linking against Lapack (`--with-lapack` or `--with-lapack-lflags`),
the macro checks for installed versions of Lapack on the system, e.g., `-llapack -lblas` are tried on Linux systems, the `Accelerate` framework is checked for on macOS, the Sun Performance Library is checked on Solaris machines, and linking against Intel MKL libraries is tried.

The first argument of this macros takes the same role as in previously mentioned macros.
That is,
```
AC_COIN_CHK_LAPACK(CoinUtilsLib)
```
will add flags to link against Lapack to `COINUTILSLIB_LFLAGS`.

Additionally, the following variables are setup:
* A shell variable `coin_has_lapack` is set to `yes` or `no`.
* An automake conditional `COIN_HAS_LAPACK` is defined.
* A `#define PROJECT_HAS_LAPACK` for configuration headers is defined, where `PACKAGE` is replaced by the name of the package that is configured in all capital letters.
* Naming convention for Lapack functions is figured out and macros `PACKAGE_LAPACK_FUNC` and `PACKAGE_LAPACK_FUNC_` are added for configuration headers.


A separate macro to check for BLAS only has been omitted, but it should be assumed that availablity of Lapack is sufficient to indicate availability of Blas.


### Finalizing compiler and linker flags

`AC_COIN_CHK_PKG` stores pkg-config dependencies for libraries in variables `X_PCFILES`.
The macro **`AC_COIN_FINALIZE_FLAGS`** can be used to retrieve the linker and compiler flags from these pkg-config files and update the corresponding `X_LFLAGS` and `X_CFLAGS` variables.
This should be done once in a final step when all dependencies of a library have been checked, since that allows to profit most from pkg-config removing duplicate linker and compiler flags.

The **`AC_COIN_FINALIZE_FLAGS`** takes as its single argument a space-separated list of library names.
For each of them:
* The current value of `X_LFLAGS` and `X_CFLAGS` is stored in new variables `X_LFLAGS_NOPC` and `X_CFLAGS_NOPC`.
  These variables will be used to setup pkg-config files of the project, as there the separation into package and library dependencies is important again.
* The compiler and linker flags for the pkg-config dependencies are evaluated and stored at the beginning of variables `X_LFLAGS` and `X_CFLAGS`.
* A configuration header variable `X_EXPORT` is defined and set to `__declspec(dllimport)` if shared libraries are build under Windows.
* `-DX_BUILD` is added to `X_CFLAGS`.
  This can be recognized in the projects header files whether the project is currently build, or the header is included by a user.

To continue the example above, using the macro
```
AC_COIN_FINALIZE_FLAGS(ClpLib)
```
could lead to the following variable values:
* `CLPLIB_CFLAGS_NOPC="-I/my/build/of/mumps/include -I/my/build/of/mumps/libseq"`
* `CLPLIB_LFLAGS_NOPC="-L/my/build/of/mumps/lib -lmumps"`
* `CLPLIB_CFLAGS="-I/usr/local/include/coin-or -I/my/build/of/mumps/include -I/my/build/of/mumps/libseq"`
* `CLPLIB_LFLAGS="-L/usr/local/lib -lCoinUtils -L/my/build/of/mumps/lib -lmumps"`
* `CLPLIB_PCFILES=coinutils`
Note that the compiler and linker flags for CoinUtils, as retrieved from coinutils.pc, have been added to `CLPLIB_CFLAGS` and `CLPLIB_LFLAGS`, respectively.


### Generation of Links for Data Files

Some unit test programs and example programs require input data files.
In a VPATH configuration (_i.e._, the directory where compilation takes place is different from the directory containing the source files) it is important to make sure that links to those data files exist so that the programs can be run in the compilation directory.

To this purpose, the **`AC_COIN_VPATH_LINK`** macro should be used for each such file, e.g.,
```
AC_COIN_VPATH_LINK(examples/VolUfl/ufl.par)
AC_COIN_VPATH_LINK(examples/VolUfl/data.gz)
```

This macro simply expands to repeated use of the `AC_CONFIG_LINKS` macro:
```
AC_DEFUN([AC_COIN_VPATH_LINK],
[
  m4_foreach_w(linkvar,[$1],[AC_CONFIG_LINKS(linkvar:linkvar)])
])
```

### Project Specific Tests

If you need to perform other tests, you might need to use further autoconf macros and/or write some `/bin/sh` code.
For this, please consult the [autoconf documentation](http://www.gnu.org/software/autoconf/manual).


## The End of the `configure.ac` File

At the end of the `configure.ac` file, we need to make sure that the **output is written**.
In COIN-OR, the bottom of the file usually looks like this:

```
##############################################################################
#                   Finish up by writing all the output                   #
##############################################################################

# Here list all the files that configure should create (except for the
# configuration header file)
AC_CONFIG_FILES([Makefile
                 examples/Makefile
                 src/Makefile
                 test/Makefile
                 supersolver.pc.in])

# Here put the location and name of the configuration header file
AC_CONFIG_HEADER([inc/config.h inc/config_supersolver.h])

# Finally, we let configure write all the output...
AC_COIN_FINALIZE
```

 * The **`AC_CONFIG_FILES`** macro takes as its single argument a space-separated list of the files that are to be created from the corresponding `.in` template files.
   These are all the `Makefile`s and maybe some additional files.
   In the example shown here, the project installs a _prjct_`.pc` file, and this will also be created from a template.
   **A template file must exist for each file listed in the argument to `AC_CONFIG_FILES`.**

 * The **`AC_CONFIG_HEADER`** macro takes as its single argument a space-separated list of names of [configuration header](./autotools-intro) files that are to be created by `configure`.
   For the first file in this list, the template will be created by the autotools utility `autoheader`.
   For the remaining ones, the project manager has to provide the template files.

 * The **`AC_COIN_FINALIZE`** macro takes care of actually writing the output.
   Internally, it uses the `AC_OUTPUT` macro, but since additional actions might have to be taken, you should use `AC_COIN_FINALIZE` instead of using `AC_OUTPUT` directly.
   `AC_COIN_FINALIZE` also writes the "configuration successful" message before the `configure` script finally stops.
